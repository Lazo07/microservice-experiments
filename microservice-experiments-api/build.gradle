
plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'com.google.cloud.tools.jib' version '3.3.0'
    id 'java'
}

allprojects {

    group = 'com.experiments.microservice'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    if (project.name.contains('command-service')){
        apply plugin: 'com.google.cloud.tools.jib'

        jib {
            containerizingMode = 'exploded'

            from {
                image = "eclipse-temurin:17-alpine"
            }
            to {
                image = "lazo07/$project.name"
                tags = ['latest', project.version]
                auth {
                    username = dockerHubUsername
                    password = dockerHubPassword
                }
            }
            container {
                entrypoint = ['INHERIT']
                creationTime = 'USE_CURRENT_TIMESTAMP'
            }
        }
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}